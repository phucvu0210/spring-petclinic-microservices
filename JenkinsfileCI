pipeline {
    agent any
    environment {
        DOCKERHUB_CREDENTIALS = credentials('dockerhub-credentials')
        SHOULD_RUN_PIPELINE = 'true' // default: chạy
    }

    stages {
        stage('Checkout Code') {
            steps {
                script {
                    def branchToCheckout = env.BRANCH_NAME
                    echo "Branch hiện tại: ${branchToCheckout}"

                    if (branchToCheckout == 'main') {
                        echo "Đây là nhánh 'main'. Pipeline sẽ dừng lại và vẫn được đánh dấu SUCCESS."
                        env.SHOULD_RUN_PIPELINE = 'false'
                        return
                    }

                    git branch: branchToCheckout, url: 'https://github.com/phucvu0210/spring-petclinic-microservices.git'
                }
            }
        }

        stage('Build JAR') {
            when {
                expression { env.SHOULD_RUN_PIPELINE == 'true' }
            }
            steps {
                sh './mvnw clean package -DskipTests'
            }
        }

        stage('Build and Push Docker Images') {
            when {
                expression { env.SHOULD_RUN_PIPELINE == 'true' }
            }
            steps {
                script {
                    def commitId = sh(script: 'git rev-parse --short HEAD', returnStdout: true).trim()
                    def changedFiles = sh(script: "git diff --name-only HEAD^ HEAD || true", returnStdout: true).trim().split('\n')
                    def serviceConfigs = [
                        [name: 'admin-server', dir: 'spring-petclinic-admin-server', port: 9090],
                        [name: 'api-gateway', dir: 'spring-petclinic-api-gateway', port: 8080],
                        [name: 'config-server', dir: 'spring-petclinic-config-server', port: 8888],
                        [name: 'customers-service', dir: 'spring-petclinic-customers-service', port: 8081],
                        [name: 'discovery-server', dir: 'spring-petclinic-discovery-server', port: 8761],
                        [name: 'genai-service', dir: 'spring-petclinic-genai-service', port: 8084],
                        [name: 'vets-service', dir: 'spring-petclinic-vets-service', port: 8083],
                        [name: 'visits-service', dir: 'spring-petclinic-visits-service', port: 8082]
                    ]
                    def servicesToBuild = serviceConfigs.findAll { config ->
                        changedFiles.any { it.startsWith(config.dir + '/') }
                    }
                    if (servicesToBuild.isEmpty()) {
                        echo "No services changed. Building all services as fallback."
                        servicesToBuild = serviceConfigs
                    }

                    withCredentials([usernamePassword(credentialsId: 'dockerhub-credentials', usernameVariable: 'DOCKER_USERNAME', passwordVariable: 'DOCKER_PASSWORD')]) {
                        sh 'echo $DOCKER_PASSWORD | docker login -u $DOCKER_USERNAME --password-stdin'
                        for (def service in servicesToBuild) {
                            def imageName = "${DOCKER_USERNAME}/spring-petclinic-${service.name}:${commitId}"
                            def jarFile = sh(script: "ls ${service.dir}/target/*.jar | head -1", returnStdout: true).trim()
                            sh """
                                mkdir -p docker
                                cp ${jarFile} docker/${service.name}.jar
                                cd docker
                                docker build --build-arg ARTIFACT_NAME=${service.name} --build-arg EXPOSED_PORT=${service.port} -t ${imageName} .
                                docker push ${imageName}
                                rm ${service.name}.jar
                                cd ..
                            """
                        }
                    }
                }
            }
        }
    }

    post {
        success {
            echo "✅ Pipeline completed."
        }
    }
}
